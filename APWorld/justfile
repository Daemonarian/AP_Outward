set shell := ["pwsh", "-Command"]

venv_dir := ".venv"
venv_python := venv_dir + "/Scripts/python.exe"

archipelago_host := env_var_or_default("ARCHIPELAGO_HOST", "localhost")
archipelago_port := env_var_or_default("ARCHIPELAGO_PORT", "38281")
archipelago_password := env_var_or_default("ARCHIPELAGO_PASSWORD", "password")
archipelago_loglevel := env_var_or_default("ARCHIPELAGO_LOGLEVEL", "info")

build_dir := "build"
staging_dir := build_dir + "/staging"
deploy_dir := build_dir + "/deploy"
release_dir := "release"

archipelago_dir := "../External/Archipelago"

options_yaml := "src/outward/Outward Definitive Edition.yaml"

default:
    @just --list

clean:
    if (Test-Path -Path "{{release_dir}}" -PathType Container) { Remove-Item -Path "{{release_dir}}" -Recurse -Force }
    if (Test-Path -Path "{{build_dir}}" -PathType Container) { Remove-Item -Path "{{build_dir}}" -Recurse -Force }

clean-setup:
    @just clean
    if (Test-Path -Path "{{venv_dir}}" -PathType Container) { Remove-Item -Path "{{venv_dir}}" -Recurse -Force }

setup:
    & "py" "setup.py"

build:
    @just setup
    Write-Output "Nothing to build."

build-full:
    @just setup
    $venv_dir = (Resolve-Path -Path "{{venv_dir}}").Path; Set-Location -Path "{{archipelago_dir}}" ; & "${venv_dir}\Scripts\python.exe" setup.py build --yes

test:
    @just build
    $venv_dir = (Resolve-Path -Path "{{venv_dir}}").Path; Set-Location -Path "{{archipelago_dir}}" ; & "${venv_dir}\Scripts\pytest.exe"

stage:
    @just build
    New-Item -Path "{{staging_dir}}" -ItemType Directory -Force | Out-Null
    Copy-Item -Path "src\outward" -Destination "{{staging_dir}}" -Recurse -Force

release:
    @just stage
    New-Item -Path "{{release_dir}}" -ItemType Directory -Force | Out-Null
    Compress-Archive -Path "{{staging_dir}}\outward" -DestinationPath "{{release_dir}}\outward.apworld" -Force

install:
    @just release
    if (Test-Path -Path "{{archipelago_dir}}\worlds\outward") { Remove-Item -Path "{{archipelago_dir}}\worlds\outward" }
    New-Item -Path "{{archipelago_dir}}\custom_worlds" -ItemType Directory -Force | Out-Null
    Copy-Item -Path "{{release_dir}}\outward.apworld" -Destination "{{archipelago_dir}}\custom_worlds\outward.apworld" -Force

deploy:
    @just install
    if (Test-Path -Path "{{deploy_dir}}" -PathType Container) { Remove-Item -Path "{{deploy_dir}}" -Recurse -Force }
    New-Item -Path "{{deploy_dir}}\players" -ItemType Directory -Force | Out-Null
    Copy-Item -Path "{{options_yaml}}" -Destination "{{deploy_dir}}\players\Player1.yaml" -Force
    $generate = (Resolve-Path -Path "{{archipelago_dir}}\Generate.py").Path ; $venv_python = (Resolve-Path -Path "{{venv_python}}").Path ; Set-Location -Path "{{deploy_dir}}" ; & $venv_python $generate --player_files_path players --outputpath output
    Get-ChildItem -Path "{{deploy_dir}}\output" -Filter "*.zip" -Recurse | Expand-Archive -DestinationPath "{{deploy_dir}}\output" -Force
    Get-ChildItem -Path "{{deploy_dir}}\output" -Filter "*.archipelago" -Recurse | Copy-Item -Destination "{{deploy_dir}}\test.archipelago" -Force
    $venv_python = (Resolve-Path -Path "{{venv_python}}").Path ; $script = (Resolve-Path -Path "{{archipelago_dir}}\MultiServer.py").Path ; Start-Process -FilePath "pwsh" -ArgumentList @("-Command", "& '${venv_python}' '${script}' --host '{{archipelago_host}}' --port '{{archipelago_port}}' --password '{{archipelago_password}}' --loglevel '{{archipelago_loglevel}}' --logtime 'test.archipelago' ; Write-Host -NoNewLine 'Press any key to continue...' ; `$null = `$Host.UI.RawUI.ReadKey('NoEcho,IncludeKeyDown')") -WorkingDirectory "{{deploy_dir}}"
